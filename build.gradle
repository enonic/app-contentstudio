plugins {
    id 'com.enonic.defaults' version '2.0.1' apply false
    id 'com.enonic.xp.app' version '2.0.0' apply false
    id 'com.github.node-gradle.node' version '3.0.1' apply false
    id 'org.asciidoctor.convert' version '2.4.0'
    id 'org.aim42.htmlSanityCheck' version '1.1.3'
}

allprojects {
    apply plugin: 'com.enonic.xp.app'
    apply from: "$rootDir/gradle/env.gradle"
    apply from: "$rootDir/gradle/node.gradle"
    apply from: "$rootDir/gradle/lib-admin.gradle"

    dependencies {
        compile("com.enonic.xp:core-api:${xpVersion}")
        compile ("com.enonic.xp:portal-api:${xpVersion}")
        testCompile 'junit:junit:4.13.2'
        testCompile 'org.mockito:mockito-core:3.8.0'
    }

    app {
        systemVersion = "${xpVersion}"
        vendorName = 'Enonic AS'
        vendorUrl = 'https://enonic.com'
    }

    repositories {
        mavenLocal()
        jcenter()
        xp.enonicRepo('dev')
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

asciidoctor {
    sourceDir = file('docs')
    outputDir = file('build/docs')
    requires project.file('verbose-mode.rb')
    attributes 'source-highlighter': 'coderay',
               idseparator: '-'

    resources {
        from(sourceDir) {
            include '**/images/**'
            include '**/code/**'
            include '**/*.*'
        }
    }
}

htmlSanityCheck {
    dependsOn asciidoctor
    sourceDir = new File( "$buildDir/docs/html5" )

    sourceDocuments = fileTree(sourceDir) {
        include "index.html"
    }

    // where to put results of sanityChecks...
    checkingResultsDir = new File( "$buildDir/report/htmlchecks" )
    //checkExternalLinks = false

    // fail build on errors?
    failOnErrors = true
}

processResources.enabled = false
compileJava.enabled = false
compileTestJava.enabled = false
classes.enabled = false
unpackWebJars.enabled = false
build.enabled = false
jar.enabled = false
deploy.enabled = false
