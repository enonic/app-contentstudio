plugins {
    id 'com.enonic.defaults' version '2.0.1' apply false
    id 'com.enonic.xp.app' version '2.0.0' apply false
    id 'com.github.node-gradle.node' version '3.0.0' apply false
    id 'org.asciidoctor.convert' version '2.4.0' apply false
    id 'org.aim42.htmlSanityCheck' version '1.1.3' apply false
}

allprojects {
    apply plugin: 'com.github.node-gradle.node'
    apply plugin: 'com.enonic.xp.app'
    apply from: "$rootDir/gradle/env.gradle"
    apply from: "$rootDir/gradle/lib-admin.gradle"
    apply from: "$rootDir/gradle/node.gradle"

    dependencies {
        compile("com.enonic.xp:core-api:${xpVersion}")
        compile ("com.enonic.xp:portal-api:${xpVersion}")
        testCompile 'junit:junit:4.13.2'
        testCompile 'org.mockito:mockito-core:3.7.7'
    }

    app {
        systemVersion = "${xpVersion}"
        vendorName = 'Enonic AS'
        vendorUrl = 'https://enonic.com'
    }

    node {
        download = true
        version = '14.15.0'
    }

    repositories {
        mavenLocal()
        jcenter()
        xp.enonicRepo('dev')
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.enonic.defaults'
    apply plugin: 'com.enonic.xp.app'

    sourceSets {
        main {
            java {
                srcDirs "$rootDir/src/main/java"
            }
            resources {
                srcDirs "$rootDir/src/main/resources"
            }
        }
        test {
            java {
                srcDirs "$rootDir/src/test/java"
            }
            resources {
                srcDirs "$rootDir/src/test/resources"
            }
        }
    }

    app {
        devSourcePaths += file( "$rootDir/../lib-admin-ui/src/main/resources" )
    }

    unpackDevResources.enabled = false

    def unpackDevResourcesRoot = rootProject.tasks.getByPath(':unpackDevResources')
    def npmInstallRoot = rootProject.tasks.getByPath(':npmInstall')
    npmInstall.mustRunAfter unpackDevResourcesRoot
    npmInstall.dependsOn += npmInstallRoot
    processResources.dependsOn += unpackDevResourcesRoot

    if ( hasLibAdminUi() )
    {
        build.dependsOn += libAdminBuildTask
        clean.dependsOn += libAdminCleanTask
    }

    def webpackTask = tasks.findByPath( ':webpack' )
    if ( webpackTask != null ) webpackTask.dependsOn += unpackDevResourcesRoot
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    include "com.enonic.lib:lib-admin-ui:${libAdminUiVersion}"
}

// lib-admin-ui needs to be included, but it enables some tasks, though we only need :unpackDevResources
processResources.enabled = false
compileJava.enabled = false
compileTestJava.enabled = false
classes.enabled = false
unpackWebJars.enabled = false
build.enabled = false
jar.enabled = false
deploy.enabled = false

task lint( type: NpmTask, dependsOn: npmInstall ) {
    args = [ 'run', 'lint' ]
    inputs.files fileTree( dir: 'modules', include: 'src/main/**.*' )
    outputs.dir file('gradle')
    outputs.upToDateWhen { false }
}

if ( hasLibAdminUi() )
{
    flush.dependsOn += libAdminFlushTask
    unpackDevResources.dependsOn += libAdminBuildTask
}
