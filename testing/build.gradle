import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
import java.net.HttpURLConnection
import java.net.URL

plugins {
    id 'java'
    id 'com.enonic.defaults'
    id 'com.github.node-gradle.node'
}
group = 'com.enonic.app.contentstudio.js_testing'

configurations {
    distro
    apps {
        transitive = false
    }
}

def unpackDir = layout.buildDirectory.dir("install").get()
def distroDir = unpackDir.dir("enonic-xp-generic-$xpVersion")
def deployDir = distroDir.dir("home/deploy")
def appDir = "$projectDir/test-applications"
def xpHome = "$distroDir/home"
def configDir = "$xpHome/config"
def contentStudioFile = "$deployDir/contentstudio-${version}.jar"
def contentStudioAppUrl = project.hasProperty('contentStudioAppUrl') ? contentStudioAppUrl : "file:///$projectDir/../modules/app/build/libs/contentstudio.jar"

dependencies {
    distro "com.enonic.xp:enonic-xp-generic:$xpVersion@tgz"
    apps "com.enonic.uitest:adfs-idprovider:1.0.0-SNAPSHOT"
    apps "com.enonic.uitest:auth0-idprovider:1.0.0-SNAPSHOT"
    apps "com.enonic.uitest:contenttypes:1.0.0-SNAPSHOT"
    apps "com.enonic.uitest:myfirstapp:1.0.0-SNAPSHOT"
    apps "com.enonic.uitest:simple-page:1.0.0-SNAPSHOT"
    apps "com.enonic.uitest:test-application:1.0.0-SNAPSHOT"
}

tasks.register( 'downloadDistro', Copy ) {
    println '*********************************************************'
    println '* downloadDistro  *'
    println '*********************************************************'

    configurations.distro.each { file ->
        println "Downloading ${file.name}"
    }

    from {
        configurations.distro.collect { tarTree( it ) }
    }
    into unpackDir

    doLast {
        println 'Task downloadDistro completed successfully.'
        println 'XP distro was downloaded to ' + unpackDir
    }
}

tasks.register( 'copyConfig', Copy ) {
    println "$appDir${File.separator}common-config"
    from "$appDir${File.separator}common-config"
    include '**.*.cfg'
    include '*.properties'
    into file( configDir )
    mustRunAfter downloadDistro
}

tasks.register( 'copyCustomConfig', Copy ) {
    println "$appDir${File.separator}custom-config"
    from "$appDir${File.separator}custom-config"
    include '**.*.cfg'
    into file( configDir )
    mustRunAfter downloadDistro
}

tasks.register( 'copyProjectMultiConfig', Copy ) {
    println "$appDir${File.separator}project-multi-config"
    from "$appDir${File.separator}project-multi-config"
    include '**.*.cfg'
    into file( configDir )
    mustRunAfter downloadDistro
}

tasks.register( 'downloadApps', Copy ) {
    println '*********************************************************'
    println '* downloadApps  *'
    println '*********************************************************'
    println 'Downloading test data to ' + deployDir
    from {
        configurations.apps
    }
    into deployDir
    mustRunAfter startServer

    doLast {
        println 'Task downloadApps completed successfully.'
        println 'Downloaded artifacts:'
        configurations.apps.files.each { file ->
            println "- ${file.name}"
        }
    }
}

tasks.register( 'deployContentStudio', DefaultTask ) {
    println '*********************************************************'
    println '* deployContentStudio  *'
    println '*********************************************************'

    println 'Downloading Content Studio App from ' + contentStudioAppUrl + ' to ' + contentStudioFile

    outputs.files( contentStudioFile )
    doLast {
        println 'Task deployContentStudio completed successfully.'
        def f = new File( contentStudioFile )
        if ( !f.exists() )
        {
            new URL(contentStudioAppUrl as String).withInputStream { i -> f.withOutputStream { it << i } }
            println 'Content Studio App is downloaded.'
        }
        else
        {
            println 'Content Studio App already exists.'
        }
    }
    mustRunAfter downloadDistro
}

def process
tasks.register( 'startServer' ) {
    println '*********************************************************'
    println '* startServer  *'
    println '*********************************************************'

    dependsOn( downloadDistro, deployContentStudio )
    doLast {
        sleep(5000)
        println 'Starting XP Server...'
        def pb
        if (DefaultNativePlatform.getCurrentOperatingSystem().windows) {
            pb = new ProcessBuilder('cmd', '/c', "${distroDir}\\bin\\server.bat")
        }
        else {
            pb = new ProcessBuilder("${distroDir}/bin/server.sh")
        }
        Map<String, String> env = pb.environment()
        env.put("XP_HOME", "${xpHome}".toString())

        def logsPath = layout.buildDirectory.dir("reports/logs").get()
        mkdir logsPath.asFile
        pb.redirectOutput(logsPath.file("xp.log").asFile)
        pb.redirectErrorStream(true)
        process = pb.start()

        // Make an HTTP POST request to check if the server is up
        def retryCount = 10
        def serverUp = false
        def serverUrl = "http://localhost:2609/health"

        while (retryCount > 0 && !serverUp) {
            try {
                def url = new URL(serverUrl)
                def connection = url.openConnection() as HttpURLConnection
                connection.requestMethod = 'GET'
                connection.connectTimeout = 5000
                connection.readTimeout = 5000

                if (connection.responseCode == 200) {
                    println 'Server is up and running!'
                    serverUp = true
                } else {
                    println "Server response: ${connection.responseCode}"
                }
            } catch (Exception e) {
                println 'Waiting for server to start...'
            }

            if (!serverUp) {
                sleep(5000) // Retry every 5 seconds
                retryCount--
            }
        }

        if (!serverUp) {
            throw new GradleException("Server failed to start within the expected time!")
        }

    }
}

tasks.register( 'stopServer' ) {
    doLast {
        if (DefaultNativePlatform.getCurrentOperatingSystem().windows) {
            Runtime.getRuntime().exec("taskkill /F /T /PID " + process.pid());
        } else {
            process.destroy()
        }
    }
}

tasks.register( 'cleanup', Delete) {
    delete './build/reports/allure'
}

tasks.register( 'generateReportAndStopServer', NpmTask ) {
    args = ['run-script', 'allure-report']
    finalizedBy cleanup
    finalizedBy stopServer
}

///////////////// CHROME WDIO , startServer, copyApps
tasks.register( 'w_testInputTypes', NpmTask ) {
    dependsOn ( npmInstall, downloadApps, copyConfig, startServer )
    args = ['run-script', 'test_input_types:wdio_chrome']
    finalizedBy generateReportAndStopServer
}

tasks.register( 'w_testInputTypesLocal', NpmTask ) {
    dependsOn ( npmInstall )
    args = ['run-script', 'test_input_types:wdio_chrome']
}

tasks.register( 'w_testInputTypes_2', NpmTask ) {
    dependsOn ( npmInstall, downloadApps, copyConfig, startServer )
    args = ['run-script', 'test_input_types_2:wdio_chrome']
    finalizedBy generateReportAndStopServer
}

tasks.register( 'w_testInputTypes_2_Local', NpmTask ) {
    dependsOn ( npmInstall )
    args = ['run-script', 'test_input_types_2:wdio_chrome']
}

tasks.register( 'w_testModalDialog', NpmTask ) {
    dependsOn ( npmInstall, downloadApps, copyConfig, startServer )
    args = ['run-script', 'test_modal_dialog:wdio_chrome']
    finalizedBy generateReportAndStopServer
}

tasks.register( 'w_testModalDialogLocal', NpmTask ) {
    dependsOn ( npmInstall )
    args = ['run-script', 'test_modal_dialog:wdio_chrome']
}

tasks.register( 'w_testWizardsGrid', NpmTask ) {
    dependsOn ( npmInstall, downloadApps, copyConfig, startServer )
    args = ['run-script', 'test_wizards_grid:wdio_chrome']
    finalizedBy generateReportAndStopServer
}

tasks.register( 'w_testWizardsGridLocal', NpmTask ) {
    dependsOn ( npmInstall )
    args = ['run-script', 'test_wizards_grid:wdio_chrome']
}

tasks.register( 'w_testPublishIssues', NpmTask ) {
    dependsOn ( npmInstall, downloadApps, copyConfig, startServer )
    args = ['run-script', 'test_publish_issues:wdio_chrome']
    finalizedBy generateReportAndStopServer
}
tasks.register( 'w_testPublishIssuesLocal', NpmTask ) {
    dependsOn ( npmInstall )
    args = ['run-script', 'test_publish_issues:wdio_chrome']
}

tasks.register( 'w_testProjects', NpmTask ) {
    dependsOn ( npmInstall, downloadApps, copyConfig, startServer )
    args = ['run-script', 'test_project:wdio_chrome']
    finalizedBy generateReportAndStopServer
}

tasks.register( 'w_miscTests', NpmTask ) {
    dependsOn ( npmInstall, downloadApps, copyConfig, startServer )
    args = ['run-script', 'test_misc:wdio_chrome']
    finalizedBy generateReportAndStopServer
}

tasks.register( 'w_testProjects_2', NpmTask ) {
    dependsOn ( npmInstall, downloadApps, copyConfig, startServer )
    args = ['run-script', 'test_project_2:wdio_chrome']
    finalizedBy generateReportAndStopServer
}

tasks.register( 'testProjects_multi', NpmTask ) {
    dependsOn ( npmInstall, downloadApps, copyConfig, copyProjectMultiConfig, startServer )
    args = ['run-script', 'test_project_multi:wdio_chrome']
    finalizedBy generateReportAndStopServer
}

tasks.register( 'w_testProjectsLocal', NpmTask ) {
    dependsOn ( npmInstall )
    args = ['run-script', 'test_project:wdio_chrome']
}

tasks.register( 'w_testPageEditor', NpmTask ) {
    dependsOn ( npmInstall, downloadApps, copyConfig, startServer )
    args = ['run-script', 'test_page_editor:wdio_chrome']
    finalizedBy generateReportAndStopServer
}

tasks.register( 'w_testPageEditorLocal', NpmTask ) {
    dependsOn ( npmInstall )
    args = ['run-script', 'test_page_editor:wdio_chrome']
}

tasks.register( 'w_testExcludeDependencies', NpmTask ) {
    dependsOn ( npmInstall, downloadApps, copyConfig, copyCustomConfig, startServer )
    args = ['run-script', 'test_exclude_dependencies:wdio_chrome']
    finalizedBy generateReportAndStopServer
}
