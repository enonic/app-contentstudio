import com.enonic.uitest.server.ServerInstance

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
            jcenter()
        }
    }
    dependencies {
        classpath 'com.moowork.gradle:gradle-gulp-plugin:0.13'
        classpath "com.moowork.gradle:gradle-node-plugin:0.13"
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'com.enonic.defaults' version '1.0.3'
    id 'com.moowork.node' version '0.13'
    id 'com.moowork.gulp' version '0.13'
    id 'com.github.psxpaul.execfork' version '0.1.8'

}
group = 'com.enonic.xp.js_testing'
version = '1.0'

group = 'com.enonic.xp.js_testing'
version = '1.0'

configurations {
    distro
}

apply plugin: 'com.enonic.defaults'
apply plugin: 'com.moowork.node'
apply plugin: 'com.moowork.gulp'
ext.extProgram = new Properties()
extProgram.load( new FileInputStream( "../gradle.properties" ) )
extProgram['xpVersion']


enonic {
    publishRepo = 'inhouse'
}

ext {
    xpVersion = extProgram['xpVersion']
    unpackDir = "$buildDir${File.separator}install"
    screenshotsDir = "$buildDir${File.separator}screenshots"
    reportsDir = "$buildDir${File.separator}reports"
    distroDir = "$unpackDir${File.separator}enonic-xp-$xpVersion"
    deployDir = "$distroDir${File.separator}home${File.separator}deploy"
    appDir = "$projectDir${File.separator}test-applications"
    seleniumDir = "$projectDir${File.separator}test-applications${File.separator}selenium"
    allureResultsDir = "allure-results"
    xpHome = "$distroDir${File.separator}home"
    configDir = "$xpHome${File.separator}config"
    xpServer = new ServerInstance()
}
task ensureScreenshotsDirectory {
    doLast {
        mkdir screenshotsDir
    }
}
task copyConfig( type: Copy ) {
    println "$appDir${File.separator}common-config"
    from "$appDir${File.separator}common-config"
    include '**.*.cfg'
    include '*.properties'
    into file( configDir )
}

task copyApps( type: Copy ) {
    println '*********************************************************'
    println '* task started:copyApps  *'
    println '*********************************************************'
    println appDir
    from file( appDir )
    include '*.jar'
    into file( deployDir )
}
dependencies {
    compile "com.enonic.xp:core-api:$xpVersion"
    distro "com.enonic.xp:enonic-xp-generic:$xpVersion@zip"
}

task unpackDistro( type: Copy ) {
    from {
        configurations.distro.collect { zipTree( it ) }
    }
    into file( unpackDir )
}

task startSeleniumDaemon( type: com.github.psxpaul.task.ExecFork ) {
    commandLine = "${seleniumDir}${File.separator}selenium-server.sh"
    standardOutput = "${buildDir}${File.separator}selenium-daemon.log"
    errorOutput = "${buildDir}${File.separator}selenium-daemon-error.log"
}
task zipScreenshots( type: Zip ) {
    from screenshotsDir
    include '*'
    include '*/*'
    classifier 'screenshots'
}

task zipAllure( type: Zip ) {
    from allureResultsDir
    include '*'
    include '*/*'
    classifier 'allureResults'
}

artifacts {
    archives zipScreenshots
    archives zipAllure
}

jar.enabled = true

task startServer( dependsOn: [unpackDistro, copyConfig] ) {
    doLast {
        xpServer.installDir = file( distroDir )
        xpServer.startupDelay = 40000
        xpServer.start()
    }
    copyConfig.mustRunAfter unpackDistro
}

task stopServer {
    doLast {
        xpServer.stop()
    }
}

tasks.withType( Test ) {
    jvmArgs '-Xms128m', '-Xmx1024m'
    testLogging {
        exceptionFormat "full"
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        displayGranularity = 0
    }
}

task deleteFolders( type: Delete ) {
    delete unpackDir, screenshotsDir, reportsDir
}

task buildApplications( dependsOn: ['test-applications:simple-page:install'] )

//run the tusk in the task.js. Name of the task is 'test'
task testContentStudioApp( type: NpmTask,
                           dependsOn: [npmInstall, startServer, copyApps, startSeleniumDaemon, ensureScreenshotsDirectory] ) {
    inputs.files fileTree( dir: '.' )
    outputs.files fileTree( dir: '/test-results' )
    args = ['test']
    startServer.mustRunAfter npmInstall
    copyApps.mustRunAfter startServer
    startSeleniumDaemon.mustRunAfter copyApps
}

task testContentStudioAppLocally( type: NpmTask, dependsOn: [npmInstall, startSeleniumDaemon, ensureScreenshotsDirectory] ) {
    inputs.files fileTree( dir: '.' )
    outputs.files fileTree( dir: '/test-results' )
    args = ['test']
    startSeleniumDaemon.mustRunAfter npmInstall
}

task runSeparateTestLocally( type: Exec, dependsOn: [npmInstall, startSeleniumDaemon, ensureScreenshotsDirectory] ) {
    inputs.files fileTree( dir: '.' )
    outputs.files fileTree( dir: '/test-results' )
    if ( project.hasProperty( "t_name" ) )
    {
        println( "N_A_M_E: " + t_name );
        environment = ['t_name': t_name]
        commandLine 'node', './tasks/test-by-name.js'
        startSeleniumDaemon.mustRunAfter npmInstall
    }
}
testContentStudioApp.finalizedBy = ['stopServer']

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'http://repo.enonic.com/public'
    }
}
