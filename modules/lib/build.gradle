apply plugin: 'maven-publish'

group='com.enonic.lib'
archivesBaseName = 'lib-contentstudio'

configurations {
    devResources {
        attributes {
            attribute(LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, objects.named(LibraryElements, 'dev-resources'))
        }
    }
}

dependencies {
    devResources "com.enonic.lib:lib-admin-ui:${libAdminUiVersion}"
}

task copyDevResources {
    doLast {
        copy {
            from configurations.devResources.files.collect { zipTree( it ) }
            include 'dev/**'
            into '.xp'
        }
    }
}

npmInstall.dependsOn copyDevResources

node {
    download = true
    version = '16.0.0'
}

if ( hasLibAdminUi() )
{
    copyDevResources.dependsOn += libAdminBuildTask
    build.dependsOn += libAdminBuildTask
    clean.dependsOn += libAdminCleanTask
}

task lint( type: NpmTask, dependsOn: npmInstall ) {
    args = [ 'run', 'lint' ]
    inputs.files fileTree( dir: 'modules', include: 'src/main/**.*' )
    outputs.dir file('gradle')
    outputs.upToDateWhen { false }
}

task typescriptPkg( type: NpmTask, dependsOn: [npmInstall, lint] ) {
    def buildEnv = getEnvironmentShort()
    environment = [ 'NODE_ENV': nodeEnvironment() ]
    args = [ 'run', "build:$buildEnv" ]
    inputs.dir "$projectDir/src/main/resources/assets/"
    outputs.dir "$buildDir/resources/main/dev/$archivesBaseName"
}

task minifyPkg( type: NpmTask, dependsOn: [npmInstall, typescriptPkg] ) {
    description = 'Minify transpiled code.'
    args = [ 'run', "build:minify" ]
    inputs.dir "$buildDir/resources/main/dev/$archivesBaseName"
    outputs.dir "$buildDir/resources/main/dev/$archivesBaseName"
    mustRunAfter typescriptPkg
    enabled isProd()
}

task copyPackage( type: NodeTask, dependsOn: typescriptPkg ) {
    script = file( 'util/modify.js' )
    args = [ "--out=$buildDir/resources/main/dev/$archivesBaseName" ]
    mustRunAfter typescriptPkg
}

task typescript {
    dependsOn 'typescriptPkg'
    dependsOn 'minifyPkg'
    dependsOn 'copyPackage'
}

jar {
    exclude 'admin/**'
    exclude 'assets/**'
    exclude 'com/**'
    exclude 'i18n/**/common*'
    exclude 'lib/**'
    exclude 'services/**'
    exclude 'application.svg'
    exclude 'application.xml'
    exclude 'dev/**'

    includeEmptyDirs = false
    dependsOn typescript
}

task devJar ( type: Jar ) {
    archiveClassifier = 'dev-resources'
    from sourceSets.main.output
    include 'dev/**'
    includeEmptyDirs = false
    dependsOn typescript
}

jar.dependsOn devJar

artifacts {
    devResources devJar
}

components.java.addVariantsFromConfiguration( configurations.devResources )  {
}
